<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical"
	width="400" height="300"
	showCloseButton="false"
	title="{resourceManager.getString('localization', 'Chat')}"
	creationComplete="onCreationComplete()"
	close="onClose();">

	<mx:Metadata>
		[Event(name="newChatMessage", type="flash.events.Event")]
		[Event(name="closeButton", type="flash.events.Event")]
	</mx:Metadata>

	<mx:Script>
		<![CDATA[
			import mx.controls.Image;
			import mx.utils.StringUtil;

			/**
			 * References:
			 * ===========
			 * How to scroll to the bottom of a text area automatically:
			 *
			 * http://butterfliesandbugs.wordpress.com/2007/08/20/scrolling-to-the-bottom-of-a-container-or-textarea-automatically/
			 *
			 */

			public var otherPlayerId:String = "";
			public var newMessage:String    = "";

			private var __inPrivateMode:Boolean = true;  // The DEFAULT private mode.

			// Set method.
			public function set privateMode(val:Boolean) : void
			{
			    __inPrivateMode = val;
			}

			// Get method. 
			public function get privateMode() : Boolean
			{
				return __inPrivateMode;
			}

			/*****************************************************************
			 *
			 * Reference:
			 * ----------
			 * http://blog.olivermerk.ca/index.cfm/2007/6/17/Flex-Adding-Icons-to-the-Panel
			 *
			 *****************************************************************/

			[Embed(source="../assets/edit_clear.png")]
			[Bindable]
			public var clearImageClass:Class;

			private var _clearImage:Image;
			private var _titleHbox:HBox;

			override protected function createChildren() : void
			{
				super.createChildren();

				// Prepare an HBox in which to layout the icons.
				_clearImage = new Image();
				_clearImage.source = clearImageClass;

				_titleHbox = new HBox( );
				_titleHbox.addChild( _clearImage );
				_clearImage.addEventListener( MouseEvent.CLICK, onClearClick );

				titleBar.addChild( _titleHbox );
			}

			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				super.updateDisplayList(unscaledWidth, unscaledHeight);

				// Adjust the size so that the HBox can appear!
				_titleHbox.setActualSize( _titleHbox.getExplicitOrMeasuredWidth(),
										  _titleHbox.getExplicitOrMeasuredHeight() );
				const y:int = 4;
				const x:int = this.width - _titleHbox.width - (showCloseButton ? 40 : 12);
				_titleHbox.move(x, y);
			}

			private function onClearClick(event:MouseEvent) : void
			{
				this.clearPanel();
			}

			/*****************************************************************/

			private function onCreationComplete() : void
			{
				if ( otherPlayerId != "" )
				{
					this.title += " (" + otherPlayerId + ")";
				}

				chatInput.text = resourceManager.getString('localization', 'chat_prompt');
				_clearImage.toolTip = resourceManager.getString('localization', 'clear_window');
			}

	        public function onMessageFrom(pid:String, msg:String, bPrivate:Boolean = false) : void
	        {
	        	_displayMessageOnView(pid, msg, bPrivate);
	        }

	        public function clearPanel() : void
	        {
	        	messagesArea.htmlText = "";
	        }

			private function onClose() : void
			{
				dispatchEvent( new Event("closeButton", true /* bubble to parent */) );
			}

			private function onChatInputFocusIn(event:FocusEvent) : void
			{
				chatInput.text = "";
			}

	        public function onEnterInput(event:Event) : void
	        {
				this.newMessage = StringUtil.trim( chatInput.text );
				chatInput.text = "";

				_displayMessageOnView(Global.player.pid, this.newMessage);

				dispatchEvent( new Event("newChatMessage", true /* bubble to parent */) );
	        }

	        public function _displayMessageOnView(senderId:String, msg:String, bPrivate:Boolean = false) : void
	        {
				var senderTag:String = senderId;
				if ( __inPrivateMode || bPrivate )
				{
					senderTag = "<font color=\"#3366FF\">" + senderTag + "</font>";
				}

				messagesArea.htmlText += "<p>"
					+ (senderId == "" ? "" : "<b>" + senderTag + "</b>: ")
					+ msg + "</p>";
	        }

		]]>
	</mx:Script>

	<mx:TextArea id="messagesArea" width="100%" height="100%" editable="false" backgroundColor="#F8F6F1"
		valueCommit="messagesArea.verticalScrollPosition=messagesArea.maxVerticalScrollPosition;"/>
	<mx:HBox width="100%" horizontalGap="0">
		<mx:TextInput id="chatInput" width="100%"
			focusIn="onChatInputFocusIn(event)"
			enter="onEnterInput(event)"/>
		<mx:Image source="@Embed(source='../assets/key_enter.png')" width="22" height="22" buttonMode="true"
			click="onEnterInput(event)"/>
	</mx:HBox>

</mx:TitleWindow>
