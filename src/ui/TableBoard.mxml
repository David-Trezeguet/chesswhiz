<?xml version="1.0" encoding="utf-8"?>
<mx:Panel xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:cwui="ui.*"
	layout="horizontal" width="900" height="700"
	paddingBottom="8" paddingLeft="8" paddingRight="8" paddingTop="8">

<mx:Script>
	<![CDATA[
		import mx.controls.Label;
		import mx.controls.Image;
		import mx.controls.DataGrid;
		import hoxserver.*;

		private static const RED_PLAYER_ICON:String   = "images/redplayer.png";
		private static const BLACK_PLAYER_ICON:String = "images/blackplayer.png";
		private static const TIMER_ICON:String        = "images/player_time.png";

		private var _table:Table;

		public function display(table:Table) : void
		{
			this.title = "Table #" + table.tableId;
			_table = table;
			board.setTable(_table);
			const pref:Object = _table.getPreferences();
			board.drawBoardAndPieces(pref["boardcolor"], pref["linecolor"], pref["pieceskinindex"]);
		}

		public function displayPlayerData(player:PlayerInfo) : void
		{
			var mcPlayer:HBox = (player.getColor() === _table.getTopSideColor()) ? this.topPlayer : this.bottomPlayer;
			mcPlayer.removeAllChildren();
			var mcPlayerImage:Image = new Image();
			mcPlayerImage.source = ChessApp.BASE_URI + "res/";
			mcPlayerImage.source += (player.getColor() == "Red" ? RED_PLAYER_ICON : BLACK_PLAYER_ICON);
			mcPlayerImage.width = 32;
			mcPlayer.addChild(mcPlayerImage);
			Util.createTextField(mcPlayer, player.getPlayerID() + "(" + player.getScore() + ")", 45, 5, false, 0xa09e9e, "Verdana", 12);

			var timer:GameTimers = _table.getTimers(player.getColor());
			_displayTimers(player.getColor(), timer);
		}

		public function removePlayerData(color:String) : void 
		{
			var mcPlayer:HBox = (color === _table.getTopSideColor()) ? this.topPlayer : this.bottomPlayer;
			mcPlayer.removeAllChildren();
			var mcTimers:HBox = (color === _table.getTopSideColor()) ? this.topTimers : this.bottomTimers;
			mcTimers.removeAllChildren();		
		}
		
		private function _displayTimers(color:String, timer:GameTimers) : void
		{
			var mcTimers:HBox = (color === _table.getTopSideColor()) ? this.topTimers : this.bottomTimers;
			mcTimers.removeAllChildren();

			var mcTimeImage:Image = new Image();
			mcTimeImage.source = ChessApp.BASE_URI + "res/" + TIMER_ICON;
			mcTimers.addChild(mcTimeImage);
			var tfTime:Label = Util.createTextField(mcTimers, timer.getTimer("game"), 30, 5, false, 0xa09e9e, "Verdana", 12);
			tfTime.name = color + "game";
			tfTime = Util.createTextField(mcTimers, timer.getTimer("move"), 70, 5, false, 0xa09e9e, "Verdana", 12);
			tfTime.name = color + "move";
			tfTime = Util.createTextField(mcTimers, timer.getTimer("extra"), 110, 5, false, 0xa09e9e, "Verdana", 12);
			tfTime.name = color + "extra";
		}
		
		public function updateTimers(color:String, timer:GameTimers) : void
		{
			var mcTimers:HBox = (color === _table.getTopSideColor()) ? this.topTimers : this.bottomTimers;
			if (mcTimers == null || mcTimers.numChildren == 0) {
				return;
			}
			var tfTime:Label = mcTimers.getChildByName(color + "game") as Label;
			if (tfTime) {
				tfTime.text = timer.getTimer("game");
			}
			tfTime = mcTimers.getChildByName(color + "move") as Label;
			if (tfTime) {
				tfTime.text = timer.getTimer("move");
			}
			tfTime = mcTimers.getChildByName(color + "extra") as Label;
			if (tfTime) {
				tfTime.text = timer.getTimer("extra");
			}
		}

		public function displayMessage(msg:String) : void
		{
			this.taMessages.text += msg + "\n";		
		}

		public function reviewMove(event:Event) : void
		{
			var button:Button = Button(event.target);
			_table.reviewMove(button.name);
		}
		
		public function displayChatMessage(pid:String, chatMsg:String) : void 
		{
			this.taChat.text  += "[" + pid + "] " + chatMsg + "\n";
		}

        public function postChatMessage(event:KeyboardEvent) : void
        {
        	if (event.keyCode != Keyboard.ENTER) {
        		return;
        	}
        	var temp:String;
			var chatMsg:String = this.taChatInput.text
			if (chatMsg.charAt(chatMsg.length - 1) == "\n") {
				temp = chatMsg.substr(0, chatMsg.length - 1);
				chatMsg = temp;
			}
			if (chatMsg.charAt(chatMsg.length - 1) == "\r") {
				temp = chatMsg.substr(0, chatMsg.length - 1);
				chatMsg = temp;
			}

			if (chatMsg.search("/debug") == 0) {
				_table.handleDebugCmd(chatMsg);
				this.taChatInput.text = "";
				return;
			}
			Global.vars.app.doTableChat(chatMsg);
			this.taChatInput.text = "";
			this.displayChatMessage(Global.vars.app.getPlayerID(), chatMsg);
        }

		public function redrawBoard(boardColor:uint, lineColor:uint, pieceSkin:int) : void 
		{
			board.drawBoardAndPieces(boardColor, lineColor, pieceSkin);
			this.board.changePiecesSkin(pieceSkin);
			var piece:Piece = this.board.getFocusPiece();
			if (piece) {
				piece.setFocus();
			}
		}
		
		public function changePieceSkin(pieceSkin:int) : void 
		{
			this.board.changePiecesSkin(pieceSkin);
			var piece:Piece = this.board.getFocusPiece();
			if (piece) {
				piece.setFocus();
			}
		}

		public function enableReviewButtons(flag:Boolean) : void 
		{
			for (var i:int = 0; i < this.mcMoves.numChildren; i++) {
				if (this.mcMoves.getChildAt(i) is Button) {
					var button:Button = (Button)(this.mcMoves.getChildAt(i));
					button.enabled = flag;
				}
			}
		}

	]]>
</mx:Script>

	<mx:VBox height="100%" width="570" verticalGap="5">
	
		<mx:HBox width="100%">
			<mx:HBox id="topPlayer" width="209" height="28" />
			<mx:Spacer width="100%" />
			<mx:HBox id="topTimers" width="197" height="28"  verticalAlign="middle"/>
		</mx:HBox>
		
		<cwui:BoardCanvas id="board" x="10" y="48" width="550" height="550" />

		<mx:HBox width="100%">
			<mx:HBox id="bottomPlayer" width="209" height="28" />
			<mx:Spacer width="100%" />
			<mx:HBox id="bottomTimers" width="197" height="28"  verticalAlign="middle"/>
		</mx:HBox>
		
		<mx:HBox id="mcMoves" x="150" y="638" width="100%" horizontalGap="3">
			<mx:Spacer width="170" />
			<mx:Button name="start" label="|&lt;" enabled="false" click="reviewMove(event)" />
			<mx:Button name="rewind" label="&lt;" enabled="false" click="reviewMove(event)" />
			<mx:Button name="forward" label="&gt;" enabled="false" click="reviewMove(event)" />
			<mx:Button name="end" label="&gt;|" enabled="false" click="reviewMove(event)" />
		</mx:HBox>
	</mx:VBox>

	<mx:Spacer width="100%" />

	<mx:VBox height="100%" width="270">
		<mx:Spacer height="30" />
		<mx:Panel title="Messages" width="100%" height="242" layout="absolute">
			<mx:TextArea id="taMessages" width="100%" height="100%" editable="false" />
		</mx:Panel>

		<mx:Panel title="Chat" width="100%" height="298" layout="vertical">
			<mx:TextArea id="taChat" width="100%" height="204" editable="false"/>
			<mx:TextInput id="taChatInput" width="100%" height="40" keyDown="postChatMessage(event)" />
		</mx:Panel>
	</mx:VBox>

</mx:Panel>
