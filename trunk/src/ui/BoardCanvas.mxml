<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
	initialize="init()">
	
<mx:Script>
	<![CDATA[
		import mx.controls.Label;

		private const margin:int = 50;
		private const offset:int = 45;

		private var _cellWidth:int;
		private var _cellHeight:int;
		private var _redPieces:Array = new Array(16);
		private var _blackPieces:Array = new Array(16);
		private var _redPieceHash:Object = {};
		private var _blackPieceHash:Object = {};		
		private var _pieceMap:Array = new Array(10);
		private var _table:Table = null;
		private var _lastPieceInFocus:Piece = null;
		private var _pieceSkinIndex:int = 1;
		private var _bgColor:uint;
		private var _lineColor:uint;

		private function init() : void
		{
			_cellWidth = (width - 2 * margin)/8;
			_cellHeight = (height - 2 * margin)/9;
            
			for (var k:int = 0; k < 10; k++)
			{
				_pieceMap[k] = new Array(9);
				for (var m:int = 0; m < 9; m++)
				{
					_pieceMap[k][m] = null;
				}
			}

			// --- Create piece objects.

			var index:int = 0;
			var color:String = "Red";
			_redPieces[0]  = new Piece(index++, "chariot",  color, 9, 0, this);
			_redPieces[1]  = new Piece(index++, "horse",    color, 9, 1, this);
			_redPieces[2]  = new Piece(index++, "elephant", color, 9, 2, this);
			_redPieces[3]  = new Piece(index++, "advisor",  color, 9, 3, this);
			_redPieces[4]  = new Piece(index++, "king",     color, 9, 4, this);
			_redPieces[5]  = new Piece(index++, "advisor",  color, 9, 5, this);
			_redPieces[6]  = new Piece(index++, "elephant", color, 9, 6, this);
			_redPieces[7]  = new Piece(index++, "horse",    color, 9, 7, this);
			_redPieces[8]  = new Piece(index++, "chariot",  color, 9, 8, this);
			_redPieces[9]  = new Piece(index++, "cannon",   color, 7, 1, this);
			_redPieces[10] = new Piece(index++, "cannon",   color, 7, 7, this);

  			for (var pawn:int = 0; pawn < 5; pawn++) {
	        	_redPieces[11 + pawn] = new Piece(index++, "pawn", color, 6, 2*pawn, this);
			}
			
			index = 0;
			color = "Black";
			_blackPieces[0]  = new Piece(index++, "chariot",  color, 0, 0, this);
			_blackPieces[1]  = new Piece(index++, "horse",    color, 0, 1, this);
			_blackPieces[2]  = new Piece(index++, "elephant", color, 0, 2, this);
			_blackPieces[3]  = new Piece(index++, "advisor",  color, 0, 3, this);
			_blackPieces[4]  = new Piece(index++, "king",     color, 0, 4, this);
			_blackPieces[5]  = new Piece(index++, "advisor",  color, 0, 5, this);
			_blackPieces[6]  = new Piece(index++, "elephant", color, 0, 6, this);
			_blackPieces[7]  = new Piece(index++, "horse",    color, 0, 7, this);
			_blackPieces[8]  = new Piece(index++, "chariot",  color, 0, 8, this);
			_blackPieces[9]  = new Piece(index++, "cannon",   color, 2, 1, this);
			_blackPieces[10] = new Piece(index++, "cannon",   color, 2, 7, this);
	
  			for (pawn = 0; pawn < 5; pawn++) {
	        	_blackPieces[11 + pawn] = new Piece(index++, "pawn", color, 3, 2*pawn, this);
			}

 			// --- Initialize piece map.

			_pieceMap[0][0] = _blackPieces[0];
			_pieceMap[0][1] = _blackPieces[1];
			_pieceMap[0][2] = _blackPieces[2];
			_pieceMap[0][3] = _blackPieces[3];
			_pieceMap[0][4] = _blackPieces[4];
			_pieceMap[0][5] = _blackPieces[5];
			_pieceMap[0][6] = _blackPieces[6];
			_pieceMap[0][7] = _blackPieces[7];
			_pieceMap[0][8] = _blackPieces[8];
			_pieceMap[2][1] = _blackPieces[9];
			_pieceMap[2][7] = _blackPieces[10];
			for (pawn = 0; pawn < 5; pawn++) {
	        	_pieceMap[3][2*pawn] = _blackPieces[11 + pawn];
			}

			_pieceMap[9][0] = _redPieces[0];
			_pieceMap[9][1] = _redPieces[1];
			_pieceMap[9][2] = _redPieces[2];
			_pieceMap[9][3] = _redPieces[3];
			_pieceMap[9][4] = _redPieces[4];
			_pieceMap[9][5] = _redPieces[5];
			_pieceMap[9][6] = _redPieces[6];
			_pieceMap[9][7] = _redPieces[7];
			_pieceMap[9][8] = _redPieces[8];
			_pieceMap[7][1] = _redPieces[9];
			_pieceMap[7][7] = _redPieces[10];
			for (pawn = 0; pawn < 5; pawn++) {
	        	_pieceMap[6][2*pawn] = _redPieces[11 + pawn];
			}

			// --- Initialize piece hash.

			_redPieceHash["king"]     = [ _redPieces[4] ];
			_redPieceHash["chariot"]  = [ _redPieces[0], _redPieces[8] ];
			_redPieceHash["horse"]    = [ _redPieces[1], _redPieces[7] ];
			_redPieceHash["elephant"] = [ _redPieces[2], _redPieces[6] ];
			_redPieceHash["advisor"]  = [ _redPieces[3], _redPieces[5] ];
			_redPieceHash["cannon"]   = [ _redPieces[9], _redPieces[10] ];
			_redPieceHash["pawn"]     = [ _redPieces[11], _redPieces[12],
			                              _redPieces[13], _redPieces[14],
			                              _redPieces[15] ];
			_blackPieceHash["king"]     = [ _blackPieces[4] ];
			_blackPieceHash["chariot"]  = [ _blackPieces[0], _blackPieces[8] ];
			_blackPieceHash["horse"]    = [ _blackPieces[1], _blackPieces[7] ];
			_blackPieceHash["elephant"] = [ _blackPieces[2], _blackPieces[6] ];
			_blackPieceHash["advisor"]  = [ _blackPieces[3], _blackPieces[5] ];
			_blackPieceHash["cannon"]   = [ _blackPieces[9], _blackPieces[10] ];
			_blackPieceHash["pawn"]     = [ _blackPieces[11], _blackPieces[12],
			                                _blackPieces[13], _blackPieces[14],
			                                _blackPieces[15] ];
		}

		public function setTable(table:Table) : void { _table = table; }
		public function getTable() : Table { return _table; }

		public function drawBoardAndPieces(bgColor:uint, lineColor:uint, pieceSkinIndex:int) : void
		{
			drawBoard(bgColor, lineColor);

			_pieceSkinIndex = pieceSkinIndex;
			_displayPieces();	
		}

		public function drawBoard(bgColor:uint, lineColor:uint) : void
		{
			_bgColor        = bgColor;
			_lineColor      = lineColor;

			this.graphics.lineStyle(2, _lineColor);
			this.graphics.beginFill(_bgColor);
			this.graphics.drawRect(0, 0, this.width, this.height);
			this.graphics.endFill();

			var i:int;
			for (i = 0; i < 10; i++)
			{
				_drawLine(offset, offset+i*_cellHeight, offset + 8*_cellWidth, offset + i*_cellHeight);
			}
	
			for (i = 0; i < 9; i++)
			{
				if (i == 0 || i == 8) {
					_drawLine(offset + _cellWidth*i, offset, offset + _cellWidth*i, offset + _cellHeight*9);
				} else {
					_drawLine(offset + _cellWidth*i, offset, offset + _cellWidth*i, offset + _cellHeight*4);
					_drawLine(offset + _cellWidth*i, offset + 5*_cellHeight, offset + _cellWidth*i, offset + _cellHeight*9);
				}
			}
	
			_drawLine(offset + 3*_cellWidth, offset, offset + 5*_cellWidth, offset + _cellHeight*2);
			_drawLine(offset + 5*_cellWidth, offset, offset + 3*_cellWidth, offset + _cellHeight*2);
			_drawLine(offset + 3*_cellWidth, offset + 7*_cellHeight, offset + 5*_cellWidth, offset + _cellHeight*9);
			_drawLine(offset + 5*_cellWidth, offset + 7*_cellHeight, offset + 3*_cellWidth, offset + _cellHeight*9);

			const offsetLeft:int = offset;
	        const offsetTop:int = offset;
			const bDescending:Boolean = (_table.getTopSideColor() == "Red");
			_drawHeaderRow(offsetLeft, offsetTop, bDescending);
			_drawHeaderRow(offset + _cellWidth*9 + 15, offset, bDescending);
			_drawHeaderColumn(offsetLeft, offsetTop, bDescending);
			_drawHeaderColumn(offsetLeft, offsetTop + 10*_cellHeight + 20, bDescending);
		}

		private function _displayPieces() : void
		{
			for (var i:int = 0; i < 16; i++) {
				_redPieces[i].draw(offset, _cellWidth, _cellHeight, _pieceSkinIndex);
				_blackPieces[i].draw(offset, _cellWidth, _cellHeight, _pieceSkinIndex);
			}
		}

		private function _drawLine(startX:int, startY:int, endX:int, endY:int):void
		{
			this.graphics.lineStyle(1, _lineColor);
			this.graphics.moveTo(startX, startY);
			this.graphics.lineTo(endX, endY);
		}

		private function _drawHeaderRow(offsetLeft:int, offsetTop:int, bDescending:Boolean) : void
		{
			const ROWS:int  = 10;
			var   top:int   = 0;
			const left:int  = offsetLeft - 40;
			var   start:int = (bDescending ? 0 : ROWS - 1);

			for (var i:int = 0; i < ROWS; i++)
			{
				top = offsetTop + (i * _cellHeight) - 6; 
				_addTextField("" + start, left, top, _lineColor, 12);
				if (bDescending) { start++; }
				else             { start--; }
			}
		}

		private function _drawHeaderColumn(offsetLeft:int, offsetTop:int, bDescending:Boolean) : void
		{
			const COLS:int  = 9;
			const top:int   = offsetTop - 45;
			var   left:int  = 0;
			var   start:int = (bDescending ? COLS - 1 : 0);

			for (var i:int = 0; i < COLS; i++)
			{
				left = offsetLeft + (i * _cellWidth) - 6;
				_addTextField(String.fromCharCode(97 + start), left, top, _lineColor, 12);
				if (bDescending) { start--; }
				else             { start++; }
			}
		}

		private function _addTextField(text:String, x:Number, y:Number,
									   color:uint, fontSize:uint, font:String = "Verdana") : void
		{
			var label:Label = new Label();
			label.x = x;
			label.y = y;
			label.text = text;
			label.setStyle("color", color);
			label.setStyle("fontFamily", font);
			label.setStyle("fontSize", fontSize);

			this.addChild(label);
		}

		public function getNearestCell(x:int, y:int, radius:int):Position
		{
			trace("x: " + x + " y: " + y + ", mouse x: " + this.mouseX + " y: " + this.mouseY);
			var pos:Position = new Position(-1, -1);

			var yRow:int;
			for (var i:int = 0; i < 10; i++)
			{
				yRow = offset + _cellHeight * i;
				if (Math.abs(this.mouseY - yRow) < radius)
				{
					pos.row = i;
					break;
				}
			}
			
			var xColumn:int;
			for (i = 0; i < 9; i++)
			{
				xColumn = offset + _cellWidth * i;
				if (Math.abs(this.mouseX - xColumn) < radius)
				{
					pos.column = i;				
					break;
				}
			}

			return pos;
		}

		public function getX(col:int) : int { return offset + col * _cellWidth; }
		public function getY(row:int) : int { return offset + row * _cellHeight; }

		public function getPieceByPos(pos:Position):Piece
		{
			return _pieceMap[pos.row][pos.column];
		}

		public function getPiece(color:String, type:String) : Array {
			return (color == "Red") ? _redPieceHash[type] : _blackPieceHash[type];
		}

		public function getPieceByIndex(color:String, index:String) : Piece {
			return (color == "Red" ? _redPieces[index] : _blackPieces[index]);
		}

		public function getInterveningPiece(curPos:Position, newPos:Position) : int
		{
			var numPieces:int = 0;
			var newRow:int = newPos.row;
			var newCol:int = newPos.column;
			var curRow:int = curPos.row;
			var curCol:int = curPos.column;
			var rowDiff:int = Math.abs(curRow - newRow);
			var colDiff:int = Math.abs(curCol - newCol);
			var move:int = 0;
			if (rowDiff > 0) {
				if (colDiff > 0) {
					if ((rowDiff == 1 && colDiff == 2) || (rowDiff == 2 && colDiff == 1)) {
						move = 3; // L shape move
					} else if (rowDiff == colDiff) {
						move = 2; // Diagnol move
					} else {
						move = 4; // Steep move
					}
				} else {
					move = 1; // Vertical move
				}
			} else {
				move = 0; // Horizontal move
			}
			var startCol:int = curCol;
			if (curCol > newCol) {
				startCol = newCol;
			}
			var startRow:int = curRow;
			if (curRow > newRow) {
				startRow = newRow;
			}
			var i:int = 0;
			if (move == 0) {
				for (i = 1; i < colDiff; i++) {
					if (_pieceMap[curRow][startCol + i] != null) {
						numPieces++;
					}
				}
			} else if (move == 1) {
				for (i = 1; i < rowDiff; i++) {
					if (_pieceMap[startRow + i][curCol] != null) {
						numPieces++;
					}
				}
			} else if (move == 2) {
				if (curRow < newRow) {
					for (i = 1; i < rowDiff; i++) {
						if (curCol < newCol) {
							if (_pieceMap[curRow + i][curCol + i] != null) {
								numPieces++;
							}
						} else {
							if (_pieceMap[curRow + i][curCol - i] != null) {
								numPieces++;
							}
						}
					}
				} else {
					for (i = 1; i < rowDiff; i++) {
						if (curCol < newCol) {
							if (_pieceMap[curRow - i][curCol + i] != null) {
								numPieces++;
							}
						} else {
							if (_pieceMap[curRow - i][curCol - i] != null) {
								numPieces++;
							}
						}
					}
				}
			}  else if (move == 3) {
				if (rowDiff == 1 && colDiff == 2) {
					if (curCol > newCol) {
						if (_pieceMap[curRow][curCol - 1] != null) {
							numPieces++;
						}
					} else {
						if (_pieceMap[curRow][curCol + 1] != null) {
							numPieces++;
						}
					}
				} else {
					if (curRow > newRow) {
						if (_pieceMap[curRow - 1][curCol] != null) {
							numPieces++;
						}
					} else {
						if (_pieceMap[curRow + 1][curCol] != null) {
							numPieces++;
						}
					}
				}
			}

			return numPieces;
		}		

		public function isMySide(color:String, pos:Position) : Boolean
		{
			return (     (color == "Black" && pos.row < 5 ) 
			         || ( color == "Red"   && pos.row >= 5 ) );
		}

		public function updatePieceMapState(piece:Piece, oldPos:Position, newPos:Position) : void
		{
			_pieceMap[newPos.row][newPos.column] = piece;
			_pieceMap[oldPos.row][oldPos.column] = null;
		}

		public function updatePieceMap(newPos:Position, piece:Piece) : void
		{
			var curPiece:Piece = _pieceMap[newPos.row][newPos.column];
			var oldPos:Position = piece.getPosition();
			_pieceMap[newPos.row][newPos.column] = piece;
			_pieceMap[oldPos.row][oldPos.column] = null;
			if (curPiece) {
				curPiece.setCapture(true);
				curPiece.removeImage();
			}
			piece.setPosition(newPos);
		}

		public function setFocusOnPiece(piece:Piece) : void
		{
			if (_lastPieceInFocus) {
				_lastPieceInFocus.clearFocus();
				_lastPieceInFocus = null;
			}
			if (piece) {
				piece.setFocus();
				_lastPieceInFocus = piece;
			}
		}

		public function movePieceByPos(piece:Piece, newPos:Position) : void
		{
			updatePieceMap(newPos, piece);
			piece.moveImage();

			if (_lastPieceInFocus) {
				_lastPieceInFocus.clearFocus();
			}
			piece.setFocus();
			_lastPieceInFocus = piece;
		}

		public function rewindPieceByPos(piece:Piece, curPos:Position, prevPos:Position, capturedPiece:Piece) : void
		{
			_pieceMap[prevPos.row][prevPos.column] = piece;
			_pieceMap[curPos.row][curPos.column] = capturedPiece;
			if (_lastPieceInFocus) {
				_lastPieceInFocus.clearFocus();
			}
			piece.setPosition(prevPos);
			piece.moveImage();
			if (capturedPiece) {
				capturedPiece.setCapture(false);
				capturedPiece.setPosition(curPos);
				capturedPiece.draw(offset, _cellWidth, _cellHeight, _pieceSkinIndex);
				capturedPiece.setFocus();
				_lastPieceInFocus = capturedPiece;
			}
			else {
				piece.setFocus();
				_lastPieceInFocus = piece;
			}
		}

		public function getViewPosition(pos:Position) : Position
		{
			return ( _table.getTopSideColor() == "Black" // normal view?
					? new Position( pos.row, pos.column )
					: new Position( Math.abs(pos.row - 9), Math.abs(pos.column - 8) ) );
		}

		public function displayStatus(status:String) : void {
			_addTextField(status, this.getX(1), this.getY(4) + 10, 0x33CCFF, 18);
		}

		public function enablePieceEvents(color:String) : void
		{
			var pieces:Array = (color == "Red" ? _redPieces : _blackPieces);
			for each (var piece:Piece in pieces) {
				piece.enableEvents();
			}
		}

		public function disablePieceEvents(color:String) : void
		{
			var pieces:Array = (color == "Red" ? _redPieces : _blackPieces);
			for each (var piece:Piece in pieces) {
				piece.disableEvents();
			}	
		}

		public function reDraw(changeSet:Array, focusPiece:Piece) : void
		{
			if (changeSet.length == 0) {
				return;
			}
			var piece:Piece;
			var captured:Boolean;
			var pos1:Position;
			var pos2:Position;
			var color:String;
			var index:int;

			for (var i:int = 0; i < changeSet.length; i++) {
				color = changeSet[i][0];
				index = parseInt(changeSet[i][1]);
				piece = (color == "Red" ? _redPieces[index] : _blackPieces[index]);
				captured = changeSet[i][4];
				pos1 = piece.getPosition();
				pos2 = new Position(changeSet[i][2], changeSet[i][3]);
				if (!captured) {
					if (piece.isCaptured()) {
						piece.setPosition(pos2);
						piece.draw(offset, _cellWidth, _cellHeight, _pieceSkinIndex);
					}
					else {
						if (pos1.row != pos2.row || pos1.column != pos2.column) {
							piece.removeImage();
							piece.setPosition(pos2);
							piece.draw(offset, _cellWidth, _cellHeight, _pieceSkinIndex);
						}
					}
					piece.setCapture(false);
				} else {
					if (!piece.isCaptured()) {
						piece.removeImage();
					}
					piece.setPosition(pos2);
					piece.setCapture(true);
				}
			}

			if (focusPiece != null) {
				if (_lastPieceInFocus) {
					_lastPieceInFocus.clearFocus();
				}
				focusPiece.setFocus();
				_lastPieceInFocus = focusPiece;
			}
		}
		
		public function getPieceMapInfo() : String
		{
			var result:String = "piecemap: \n";
			var line:String = "";
			var piece:Piece = null;
			var k:int = 0;
			var m:int = 0;
			var pos:Position = null;
			for(k = 0; k < 10; k++)
			{
				for(m = 0; m < 9; m++)
				{
					piece = _pieceMap[k][m];
					if (piece != null) {
						pos = piece.getPosition();
						line = pos.toString() + ": " + piece.getColor() + " " + piece.getType() + "\n";
						result += line;
					}
				}
			}
			return result;
		}
		
		public function getRedPiecesInfo() : String
		{
			var result:String = "redpieces: \n";
			var line:String = "";
			var piece:Piece = null;
			var pos:Position = null;
			for (var i:int = 0; i < _redPieces.length; i++) {
				piece = _redPieces[i];
				pos = piece.getPosition();
				line = "" + piece.getType() + " " + pos.toString() + " " + (piece.isCaptured() ? "captured" : "")  + "\n";
				result += line;
			}
			return result;
		}

		public function getBlackPiecesInfo() : String
		{
			var result:String = "blackpieces: \n";
			var line:String = "";
			var piece:Piece = null;
			var pos:Position = null;
			for (var i:int = 0; i < _blackPieces.length; i++) {
				piece = _blackPieces[i];
				pos = piece.getPosition();
				line = "" + piece.getType() + " " + pos.toString() + " " + (piece.isCaptured() ? "captured" : "")  + "\n";
				result += line;
			}
			return result;
		}
		
		public function changePiecesSkin(skinIndex:int) : void
		{
			_pieceSkinIndex = skinIndex;

			var piece:Piece = null;
			for (var i:int = 0; i < 16; i++)
			{
				piece = _redPieces[i];
				if ( ! piece.isCaptured() ) {
					piece.changeSkinIndex(_pieceSkinIndex);
				}
				piece = _blackPieces[i];
				if ( ! piece.isCaptured() ) {
					piece.changeSkinIndex(_pieceSkinIndex);
				}
			}

			if (_lastPieceInFocus) {
				_lastPieceInFocus.setFocus();
			}
		}

	]]>
</mx:Script>
	
</mx:Canvas>
